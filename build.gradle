plugins {
    id "groovy"
    id "java-gradle-plugin"
    id "maven-publish"
    id "com.gradle.plugin-publish" version "0.11.0"
    id "com.github.ben-manes.versions" version "0.28.0"
}

apply from: "jacoco.gradle"

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    implementation "de.undercouch:gradle-download-task:4.1.1"
    testImplementation "junit:junit:4.13"
    testImplementation platform("org.spockframework:spock-bom:2.0-M2-groovy-2.5")
    testImplementation "org.spockframework:spock-core"
    testImplementation "org.spockframework:spock-junit4"
}

test {
    useJUnitPlatform()
}

pluginBundle {
    website = "https://github.com/EndlessCodeGroup/BukkitGradle"
    vcsUrl = "${website}.git"
    description = this.description
    tags = ["minecraft", "bukkit", "plugin", "spigot", "paper"]

    plugins {
        bukkitGradle {
            id = "ru.endlesscode.bukkitgradle"
            displayName = "Bukkit Plugin development Gradle integration"
            description = "Gradle plugin providing integration for easier Bukkit plugins developement."
        }
    }
}
