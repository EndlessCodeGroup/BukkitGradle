plugins {
    id "groovy"
    id "idea"

    id "java-gradle-plugin"
    id "com.gradle.plugin-publish" version "0.9.7"
}

apply from: "jacoco.gradle"

group "ru.endlesscode"
description "Bukkit Gradle integration plugins"
version "0.6.3"

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile group: "de.undercouch", name: "gradle-download-task", version: "3.2.+"
    testCompile group: "junit", name: "junit", version: "4.12"
}

pluginBundle {
    website = "https://github.com/EndlessCodeGroup/BukkitGradle"
    vcsUrl = website
    description = this.description
    tags = ["minecraft", "bukkit", "plugin"]

    plugins {
        bukkitPlugin {
            id = "ru.endlesscode.bukkitgradle"
            displayName = "Bukkit Plugin Gradle integration"
            description = "Gradle plugin providing integration for plugins made for the Bukkit platform"
        }
    }
}